import org.gradle.util.GradleVersion

apply plugin: "groovy-gradle-plugin"

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.spring.io/release" }
    maven { url "https://maven.aliyun.com/repository/public" }
    maven { url "https://mirrors.cloud.tencent.com/nexus/repository/maven-public" }
}

ext {
    bootVersion = libs.versions.springBoot.get()
    jreleaserhVersion = libs.versions.jreleaserhVersion.get()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-gradle-plugin:$bootVersion"
    implementation "org.jreleaser:jreleaser-gradle-plugin:$jreleaserhVersion"
}


gradlePlugin {
    plugins {
        create("bomPlugin") {
            id = "sh.rime.bom"
            implementationClass = "sh.rime.plugin.BomPlugin"
        }
        create("modulePlugin") {
            id = "sh.rime.module"
            implementationClass = "sh.rime.plugin.ModulePlugin"
        }
        create("commonPlugin") {
            id = "sh.rime.common"
            implementationClass = "sh.rime.plugin.CommonPlugin"
        }
        create("rootProjectPlugin") {
            id = "sh.rime.root"
            implementationClass = "sh.rime.plugin.RootPlugin"
        }
        create("servicePlugin") {
            id = "sh.rime.service"
            implementationClass = "sh.rime.plugin.ServicePlugin"
        }
        create("deployedPlugin") {
            id = "sh.rime.mvn.deployed"
            implementationClass = "sh.rime.plugin.maven.DeployedPlugin"
        }
    }
}

tasks.withType(Jar).configureEach {
    manifest.attributes.putIfAbsent(
            "Created-By",
            System.getProperty("java.version") + " (" + System.getProperty("java.specification.vendor") + ")"
    )
    manifest.attributes.putIfAbsent("Gradle-Version", GradleVersion.current())
}