import org.jreleaser.gradle.plugin.tasks.JReleaserFullReleaseTask

plugins {
    id "sh.rime.root"
    id 'org.jreleaser'
    id 'jacoco'
}


ext {
    bom = Set.of(project(":shore-bom"))
    modules = (subprojects.findAll { it.buildFile.exists() } - (bom))
}

allprojects {
    group = 'sh.rime.reactor'
}

configure(modules) {
    apply plugin: "sh.rime.module"
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'

    dependencies {
        management platform(project(":shore-bom"))
        compileProcessor "org.projectlombok:lombok"
        compileProcessor "org.springframework.boot:spring-boot-configuration-processor"
        compileProcessor "org.springframework.boot:spring-boot-autoconfigure-processor"
        testImplementation "org.springframework:spring-tx"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "io.projectreactor:reactor-test"
    }

    test {
        finalizedBy jacocoTestReport
        useJUnitPlatform()
        testLogging {
            minGranularity = 3
            events "passed", "skipped", "failed"
        }
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = false
            html.required = true
        }
    }

}


subprojects {
    apply plugin: 'maven-publish'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name.set(project.name)
                    def projectDescription = project.description
                    if (projectDescription == null || projectDescription.isBlank()) {
                        projectDescription = project.name.replaceAll("-", " ")
                    }
                    description.set(projectDescription)
                    def rootProjectName = project.rootProject.name
                    url.set("https://github.com/youtaqiu/" + rootProjectName + "/")
                    licenses {
                        license {
                            name.set("The Apache License, Version 2.0")
                            url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                        }
                    }
                    developers {
                        developer {
                            name.set("rime")
                            email.set("k@rime.sh")
                        }
                    }
                    scm {
                        connection.set("git@github.com:youtaqiu/" + rootProjectName + ".git")
                        url.set("https://github.com/youtaqiu/" + rootProjectName + "/")
                    }
                }
            }
        }

        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
    jreleaser {
        deploy {
            maven {
                mavenCentral(({
                    sonatype {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        var file = layout.buildDirectory.dir('staging-deploy')
                        stagingRepository(file.get().toString())
                    }
                } as Closure<Void>))
            }
        }
    }

    tasks.register("conditionalJReleaserDeploy", JReleaserFullReleaseTask) {
        onlyIf {
            layout.buildDirectory.get().asFile.toPath().resolve('jreleaser').toFile().exists()
        }
        doFirst {
            println "Executing JReleaser deployment because jreleaser directory exists."
        }
        doLast {
            println "JReleaser deployment completed."
        }
    }
}

configure(allprojects) {
    repositories {
        maven { url "https://maven.aliyun.com/repository/public/" }
        maven { url "https://mirrors.cloud.tencent.com/nexus/repository/maven-public/" }
        maven { url "https://repo.spring.io/release" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}


jreleaser {
    signing(({
        active = 'ALWAYS'
        armored = true
    } as Closure<Void>))
}
